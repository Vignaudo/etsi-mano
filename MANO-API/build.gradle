plugins {
	id 'war'
	id 'org.springframework.boot' version '2.3.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'com.gorylenko.gradle-git-properties' version '2.2.3'
	id "org.liquibase.gradle" version "2.0.4"
}
apply plugin: 'jacoco'
apply plugin: 'liquibase'


repositories {

	maven {
		url 'http://repo.spring.io/plugins-release/'
		url 'https://repository.jboss.org/maven2/'
		url 'https://maven.repository.redhat.com/ga/'
		url 'https://repo1.maven.org/maven2/'
	}

	mavenCentral()
}

sourceSets {
	integrationTests {
		java {
			srcDir file("src/test/integration")
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
		}
	}
}

configurations {
	integrationTestsCompile.extendsFrom testCompile
	integrationTestsRuntime.extendsFrom testRuntime
}
configurations.all {
	resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

task integrationTests(type: Test) {
	testClassesDirs = sourceSets.integrationTests.output.classesDirs
	classpath = sourceSets.integrationTests.runtimeClasspath
	outputs.upToDateWhen { false }
}

gitProperties {
	gitPropertiesName = 'git.properties'
	dateFormat = "yyyy-MM-dd'T'HH:mmZ"
	dateFormatTimeZone = 'UTC'
}

group = 'com.ubiqube'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_11
project.compileJava.dependsOn(generateGitProperties)

ext {
	mainUrl = 'jdbc:postgresql://mano-db/mano'
	mainUsername = 'mano'
	mainPassword = 'mano'
}

liquibase {
	activities {
		main {
			changeLogFile 'src/main/resources/db/changelog/changes/v0001.groovy'
			url project.ext.mainUrl
			username project.ext.mainUsername
			password project.ext.mainPassword
			referenceUrl "hibernate:spring:com.ubiqube.etsi.mano.dao?dialect=org.hibernate.dialect.PostgreSQL93Dialect&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy&hibernate.implicit_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringImplicitNamingStrategy"
		}
	}
}

dependencies {
	compileOnly("org.springframework.boot:spring-boot-devtools")

	liquibaseRuntime 'org.liquibase:liquibase-core:3.10.0'
	liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:2.1.2'
	liquibaseRuntime 'org.postgresql:postgresql:42.2.16'
	liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate5:3.10.0'
	liquibaseRuntime 'org.springframework.boot:spring-boot-starter-data-jpa'
	liquibaseRuntime sourceSets.main.output

	testImplementation('org.junit.jupiter:junit-jupiter:5.7.0')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.springframework.security:spring-security-test')
	testCompile group: 'commons-jxpath', name: 'commons-jxpath', version: '1.3'
	testCompile 'com.h2database:h2'
	testCompile 'org.jeasy:easy-random-core:4.2.0'
	testCompile group: 'uk.co.jemos.podam', name: 'podam', version: '7.2.5.RELEASE'

	// Demo stuff
	compile 'com.ubiqube.mano:tosca-vnfd-class:0.1.0-SNAPSHOT'
	compile 'com.ubiqube.mano:tosca-nsd-class:0.1.0-SNAPSHOT'

	// Tosca.
	compile 'com.ubiqube.mano:mano-pkg-tosca:0.0.1-SNAPSHOT'
	compile 'com.ubiqube.mano:mano-vim-openstack:0.0.1-SNAPSHOT'
	compile 'com.ubiqube.mano:nfvo-core:0.0.1-SNAPSHOT'
	// ETSI-MANO 2.6.1 APIs.
	compile group: 'com.ubiqube.mano', name: 'etsi-nfvo-261', version: '1.0-SNAPSHOT'
	compile group: 'com.ubiqube.mano', name: 'etsi-vnfm-261', version: '1.0-SNAPSHOT'
}
bootWar {
	manifest {
		attributes(
		 'Dependencies': 'jdk.unsupported'
		 )
	 }
}
springBoot {
	mainClassName = 'com.ubiqube.etsi.mano.Application'
}
/*
java {
	withJavadocJar()
	withSourcesJar()
}
*/
test {
	useJUnitPlatform() {
		excludeTags 'Remote'
	}
}
task copyTestResources(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/src/test/resources"
}
processTestResources.dependsOn copyTestResources
